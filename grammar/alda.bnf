score                   = <ows> global-attributes? part+
part                    = calls <ows> music-data
calls                   = name
                          (<ows> <"/"> <ows> name)*
                          (<ows> nickname)?
                          <ows> <":">
<acceptable-name>       = #"[a-zA-Z]{2}[\w\-+'()]*"
name                    = acceptable-name
nickname                = <"\""> acceptable-name <"\"">

<music-data>            = (voices | event)*
<event>                 = chord | note | rest | octave-change |
                          attribute-changes | marker | at-marker | barline
voices                  = voice+ (<voice-zero> | <#"\z">)
voice                   = voice-number event*
voice-number            = <"V"> #"[1-9]\d*" <":"> <ows>
<voice-zero>            = <"V0:"> <ows>

chord                   = (note | rest) subchord+
<subchord>              = <"/"> <ows> (octave-change | attribute-changes)*
                                <ows> (note | rest) <ows>
note                    = pitch duration? <ows> slur?
rest                    = <"r"> duration? <ows>

pitch                   = pitch-notation
pitch-notation          = !name #"[a-g][+-=]*"
duration                = note-length <ows> (tie <ows> note-length <ows>)* slur?
note-length             = number dots?
number                  = #"-?[0-9]+"
dots                    = #"\.+"
<tie>                   = <"~">
slur                    = <"~">

<octave-change>         = (octave-set | octave-up | octave-down) <ows>
octave-set              = <"o"> number
octave-up               = <">">
octave-down             = <"<">

key-signature           = pitch-notation subkey*
<subkey>                = <ows> pitch-notation

<global-attributes>     = <"("> <ows> global-attribute-change+ <")"> <ows>
global-attribute-change = (global-attribute-num | global-attribute-dur |
                           global-attribute-key) <ows> <#"[,;]">? <ows>
<global-attribute-num>  = attribute-num <"!"> <ows> number
<global-attribute-dur>  = attribute-dur <"!"> <ows> duration
<global-attribute-key>  = attribute-key <"!"> <ows> key-signature

<attribute-changes>     = <"("> <ows>
                          (global-attribute-change | attribute-change)+
                          <")"> <ows>
attribute-change        = (attribute-change-num | attribute-change-dur |
                          attribute-change-key) <ows> <#"[,;]">? <ows>
<attribute-change-num>  = attribute-num <ows> number
<attribute-change-dur>  = attribute-dur <ows> duration
<attribute-change-key>  = attribute-key <ows> key-signature
<attribute-num>         = #"vol(ume)?" | #"pan(ning)?" | "tempo" |
                          #"quant(iz(e|ation))?" | #"track-vol(ume)?"
<attribute-dur>         = "note-length"
<attribute-key>         = #"key(-signature)?"

marker                  = <"%"> name <ows>
at-marker               = <"@"> name <ows>

barline                 = <"|"> <ows>

ows                     = (#"\s" | comment)*

comment                 = long-comment | short-comment
long-comment            = "(*" inside-long-comment* "*)"
inside-long-comment     = !("(*" | "*)") #".|\n|\r" | long-comment
short-comment           = "#" #".*" #"(\n|\r|$)+"
