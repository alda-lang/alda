    (* score, part, calls, etc. *)

score                   = <ows> clj-expr* part+
part                    = calls <ows> music-data
calls                   = name
                          (<ows> <"/"> <ows> name)*
                          (<ows> nickname)?
                          <ows> <":">
<acceptable-name>       = #"[a-zA-Z]{2}[\w\-+'()]*"
name                    = acceptable-name
nickname                = <"\""> acceptable-name <"\"">

    (* notes, chords & other events *)

<music-data>            = <ows> (voices | event)*
<event>                 = single-event | repeat
<single-event>          = chord | note | rest | octave-change |
                          clj-expr | marker | at-marker | barline |
                          event-sequence | cram

repeat                  = single-event <ows> <"*"> <ows> positive-number <ows>

    (* voices *)

voices                  = voice+ (<voice-zero> | <#"\z"> | &part)
voice                   = voice-number event*
voice-number            = <"V"> #"[1-9]\d*" <":"> <ows>
<voice-zero>            = <"V0:"> <ows>

    (* chords, notes, rests *)

chord                   = (note | rest) subchord+
<subchord>              = <"/"> <ows> (octave-change | clj-expr)*
                                <ows> (note | rest) <ows>
note                    = pitch duration? <ows> slur?
rest                    = <"r"> duration? <ows>

    (* pitch *)

pitch                   = !name #"[a-g]" accidental*
<accidental>            = flat | sharp | natural
flat                    = "-"
sharp                   = "+"
natural                 = "="

    (* duration *)

duration                = (note-length | seconds | milliseconds)
                          <ows> barline? <ows> subduration* slur?
<subduration>           = tie <ows> barline? <ows>
                          (note-length | seconds | milliseconds) <ows>
                          barline? <ows>

seconds                 = positive-number <"s">
milliseconds            = positive-number <"ms">

note-length             = positive-number dots?
dots                    = #"\.+"
<tie>                   = <"~">
slur                    = <"~">

    (* octaves *)

<octave-change>         = (octave-set | octave-up | octave-down) <ows>
octave-set              = <"o"> number
octave-up               = <">">
octave-down             = <"<">

    (* markers, barlines *)

marker                  = <"%"> name <ows>
at-marker               = <"@"> name <ows>

barline                 = <"|"> <ows>

    (* inline clojure expressions *)

clj-character           = <"\\"> ( "newline" / "space" / "tab" /
                                   "formfeed" / "backspace" / "return" /
                                   #"(o|u)\d{4}" / #"." )

clj-string              = <"\""> inside-clj-string* <"\"">
<inside-clj-string>     = !"\"" #".|\n|\r" | "\\\""

clj-expr                = <"("> inside-clj-expr* <")"> <ows>
<inside-clj-expr>       = !( "(" | ")" | "\"" | "\\" )
                          #".|\n|\r" | clj-string | clj-character | clj-expr

    (* event sequences *)

event-sequence          = <"["> <ows> (event-sequence / event)* <ows> <"]"> <ows>

    (* cram - rhythmic section *)

cram                    = <"{"> event* <"}"> duration? <ows>

    (* numbers *)

number                  = positive-number | negative-number
positive-number         = #"[0-9]+"
negative-number         = #"-[0-9]+"

    (* comments & whitespace *)

ows                     = (#"\s" | comment)*
comment                 = "#" #".*" #"(\n|\r|$)+"
